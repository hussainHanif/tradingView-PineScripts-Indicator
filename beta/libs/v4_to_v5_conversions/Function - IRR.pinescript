//@version=5
indicator(title='Function - IRR')
//  ||-------------------------------------------------------------------------||
// IIR( input, b0 = 1, a1 = 0, b1 = 0, a2 = 0, b2 = 0, a3 = 0, b3 = 0, a4 = 0, b4 = 0 )
// FUNCTION	The function implements fast 4th-order infinite impulse response filter.
// Analytically it is:
// y[ n ] = b0 * x[ n ] + b1 * x[ n - 1 ] + b2 * x[ n - 2 ] + b3 * x[ n - 3 ] + b4 * x[ n - 4 ] + a1 * y[ n - 1 ] + a2 * y[ n - 2 ] + a3 * y[ n - 4 ] + a4 * y[ n -4 ];
// AFL equivalent:
// y = x; // init so no glitches at the beginning appear 
// for( n = 4; n < BarCount; n++ ) 
// { 
// y[ n ] = b0 * x[ n ] + 
//           b1 * x[ n - 1 ] + 
//           b2 * x[ n - 2 ] + 
//           b3 * x[ n - 3 ] + 
//           b4 * x[ n - 4 ] + 
//           a1 * y[ n - 1 ] + 
//           a2 * y[ n - 2 ] + 
//           a3 * y[ n - 4 ] + 
//           a4 * y[ n - 4 ]; 
// }


f_irr(_src, _b0, _a1, _b1, _a2, _b2, _a3, _b3, _a4, _b4) =>
//  |{-------------------------------------------------------------------------||
//  ||  Function IRR - infinite impulse response filter
//  ||  Reference:
//  ||      https://www.amibroker.com/guide/afl/iir.html
    float _x = _src
    float _y = 0.0
    if bar_index > 4
        _y := _b0 * _x[0] + _b1 * _x[1] + _b2 * _x[2] + _b3 * _x[3] + _b4 * _x[4] + _a1 * _y[1] + _a2 * _y[2] + _a3 * _y[4] + _a4 * _y[4]
        _y
    _y
//  ||---}---------------------------------------------------------------------||

int length = input(10)

//plot simple EMA:
float ema_factor = 2 / (length + 1)

float ema_irr = f_irr(close, ema_factor, 1 - ema_factor, 0, 0, 0, 0, 0, 0, 0)
float builtin_ema = ta.ema(close, length)
plot(series=ema_irr, title='irr ema', color=color.new(color.blue, 0))
plot(series=builtin_ema, title='built-in ema', color=color.new(color.red, 0))

// wilders example: 
float wilders_factor = 1 / length
float wilders = f_irr(close, wilders_factor, 1 - wilders_factor, 0, 0, 0, 0, 0, 0, 0)
plot(series=wilders, title='wilders', color=color.new(color.purple, 0))

// Ehlers Supersmoother example:
//  Amibrokers version was all kinds of messy
//  Everget version: https://www.tradingview.com/script/VdJy0yBJ-Ehlers-Super-Smoother-Filter/
float c1 = 1.41421 * 3.14159 / length
float c2 = math.exp(-c1)  //modification based evergets version //pow(2.71828, -c1)
float a1 = 2 * c2 * math.cos(c1)
float a2 = -math.pow(c2, 2)  //modification based evergets version 
float b0 = (1 - a1 - a2) / 2
float b1 = b0

float ehlerss = f_irr(close, b0, a1, b1, a2, 0, 0, 0, 0, 0)
plot(series=ehlerss, title='Ehlers Supersmoother', color=color.new(color.teal, 0))


